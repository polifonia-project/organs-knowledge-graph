PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX core:  <https://w3id.org/polifonia/ontology/core/>
PREFIX organs: <http://w3id.org/polifonia/resource/organs/>
PREFIX organ: <http://w3id.org/polifonia/ontology/organs/>
PREFIX ins: <http://w3id.org/polifonia/ontology/instrument/>

SELECT DISTINCT ?organ_iri ?label
WHERE {
    { #same location (city)
        ?organ_query_iri core:includesWhole [
            a ins:InstrumentWhole ;
            core:hasTimedLocation [
                a core:TimedLocation ;
                core:hasLocation [
                    core:hasAddress [
                        core:hasCity ?city
                    ]
                ]
            ]
        ] .
    
    ?organ_iri rdfs:label ?label ;
        core:includesWhole [
            a ins:InstrumentWhole ;
            core:hasTimedLocation [
                a core:TimedLocation ;
                core:hasLocation [
                    core:hasAddress [
                        core:hasCity ?city
                    ]
                ]
            ]
        ] .
    }
    UNION
    { #same builder (stated name)
        ?organ_query_iri core:describedBy [
            a organ:OrganProject ;
            core:definesTask [
                rdf:type core:Task ;
                core:isClassifiedBy  organ:TaskTypeBuild
            ] ;
            core:hasProjectist [
                core:hasRole organ:Builder ;
                core:involvesAgent [
                    core:hasName [
                        core:name ?builder_name
                    ]
                ]
            ]
        ] .

        ?organ_iri rdfs:label ?label ;
            core:describedBy [
                a organ:OrganProject ;
                core:definesTask [
                    rdf:type core:Task ;
                    core:isClassifiedBy  organ:TaskTypeBuild
                ] ;
                core:hasProjectist [
                    core:hasRole organ:Builder ;
                    core:involvesAgent [
                        core:hasName [
                            core:name ?builder_name
                        ]
                    ]
                ]
            ] .        
    }
    UNION { #same builder (disambiguated name)
        ?organ_query_iri core:describedBy [
            a organ:OrganProject ;
            core:definesTask [
                rdf:type core:Task ;
                core:isClassifiedBy  organ:TaskTypeBuild
            ] ;
            core:hasProjectist [
                core:hasRole organ:Builder ;
                core:involvesAgent [
                    core:hasName ?disamb_builder_name
                ]
            ]
        ] .

        ?disamb_builder_name a core:DisambiguatedName .

        ?organ_iri rdfs:label ?label ;
            core:describedBy [
                a organ:OrganProject ;
                core:definesTask [
                    rdf:type core:Task ;
                    core:isClassifiedBy  organ:TaskTypeBuild
                ] ;
                core:hasProjectist [
                    core:hasRole organ:Builder ;
                    core:involvesAgent [
                        core:hasName ?disamb_builder_name
                    ]
                ]
            ] . 
    }

    VALUES ?organ_query_iri {
        organs:Part12_063UtrechtMaria
        organs:OI-ee4e8485b89314b0555e15c5b0b7181c
        organs:FR-59350-LILLE-TEMPLE1-X
    }
}